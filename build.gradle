plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.github.ElevenHub'
version = '0.0.1'
description = 'Common Module for MSA Services'

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = '' // jar 파일 'plain' 꼬리표 제거
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    api 'org.springframework:spring-context'
    api 'org.springframework:spring-web'
    api 'org.springframework:spring-webmvc'
    api 'com.fasterxml.jackson.core:jackson-annotations'

    api 'jakarta.servlet:jakarta.servlet-api'
    api 'org.springframework.data:spring-data-jpa'
    api 'jakarta.persistence:jakarta.persistence-api'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            from components.java

            groupId = project.group
            artifactId = 'hub-eleven-common'
            version = project.version

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionOf('runtimeClasspath')
                }
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
